apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }

    defaultConfig {

        applicationId "net.derohimat.footballschedule"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName '0.1'

        buildConfigField("String", "BASE_API_URL",
                "\"${BaseApiUrl}\"")

        buildConfigField("String", "TEST_API_KEY",
                "\"${TestApiKey}\"")

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError false
        lintConfig file("$projectDir/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/MANIFEST.MF'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlin {
        experimental {
            coroutines "enable"
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.squareup.okio:okio:1.11.0'
        force "com.squareup.okhttp3:okhttp:3.8.1"
    }
}

apply from: 'dependencies.gradle'

dependencies {

    implementation supportLibs
    implementation networkLibs
    implementation rxJavaLibs
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //    Anko
    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation "org.jetbrains.anko:anko-sqlite:$anko_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19.2"
    implementation "org.jetbrains.anko:anko-coroutines:$anko_version"

    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }

    implementation "com.jakewharton:butterknife:$versions.butterKnife"
    implementation "com.jakewharton.timber:timber:$versions.timber"
    implementation "com.facebook.stetho:stetho-okhttp3:$versions.stethOkhttp"

    debugImplementation 'com.readystatesoftware.chuck:library:1.0.4'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.0.4'

    debugImplementation('com.github.ajitsing:sherlock:1.0.2@aar') {
        transitive = true
    }
    releaseImplementation('com.github.ajitsing:sherlock-no-op:1.0.2@aar')

    implementation 'com.tspoon.traceur:traceur:1.0.1'
    implementation 'com.aurelhubert:ahbottomnavigation:2.1.0'

    implementation "com.google.dagger:dagger:$versions.dagger"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    debugImplementation leakCannary
    releaseImplementation leakCannary

    kapt "com.jakewharton:butterknife-compiler:$versions.butterKnife"
    kapt daggerCompiler

}
